use crate::lang::ast::*;
use crate::lang::ast::node::*;
use crate::lang::ast::token::*;
use crate::lang::ast::lexer::*;
use crate::lang::ast::parameter_node::ParameterNode;
use crate::lang::ast::location::Location;
use crate::lang::ast::tracked_string::TrackedString;
use crate::lang::argument::SwitchStyle;
use crate::lang::ast::node::TextLiteralStyle::{Quoted, Unquoted};

grammar<'input>(input: &'input str);

pub JobList: JobListNode = {
    Separators? <l:JobListWithoutSeparator> => l,
};

pub ExprJobList: JobListNode = {
    Separators? <l:ExprJobListWithoutSeparator> => l,
};

Separators: () = {
    Separator,
    Separators Separator,
};

JobListWithoutSeparator: JobListNode = {
    <l: @L> => JobListNode {jobs: vec![], location: Location::new(l, l)},
    <j: NonEmptyJobList> Separators? => j,
};


NonEmptyJobList: JobListNode = {
    <l: @L> <mut lst:NonEmptyJobList> Separators <j:Job> <r: @R> =>  {
        lst.jobs.push(j);
        lst.location = Location::new(l, r);
        lst
    },
    <l: @L> <j: Job> <r: @R> => JobListNode {jobs: vec![j], location: Location::new(l, r)},
};

Job: JobNode = {
    <l: @L> <c:Command> <r: @R> => JobNode{commands: vec![c], location: Location::new(l, r)},
    <l: @L> <mut j:Job> Pipe Separators? <c:Command> <r: @R> => {
        j.commands.push(c);
        j.location = Location::new(l, r);
        j
    }
};

ExprJobListWithoutSeparator: JobListNode = {
    <l: @L> => JobListNode {jobs: vec![], location: Location::new(l, l)},
    <j: ExprNonEmptyJobList> Separators? => j,
};

ExprNonEmptyJobList: JobListNode = {
    <l: @L> <mut lst:ExprNonEmptyJobList> Separators <j:Expr> <r: @R> =>  {
        lst.jobs.push(JobNode::from(j));
        lst.location = Location::new(l, r);
        lst
    },
    <l: @L> <j: Expr> <r: @R> => JobListNode {jobs: vec![j.into()], location: Location::new(l, r)},
};

Expr: Box<Node> = {
    ExprAssignment,
    <j:Expr> Pipe <c:ExprAssignment> => {
        let mut jj: JobListNode = j.into();
        let idx = jj.jobs.len() - 1;
        jj.jobs[idx].commands.push(CommandNode::from(*c));
        Box::from(Node::Substitution(jj))
    },
}

ExprAssignment: Box<Node> = {
    ExprLogical,
    <i: ExprItem> <op: Equals> <e: ExprAssignment> =>
        Box::from(Node::Assignment{target: i, style: SwitchStyle::None, operation: "=".to_string(), value: e}),
    <i: ExprItem> <op: Declare> <e: ExprAssignment> =>
        Box::from(Node::Assignment{target: i, style: SwitchStyle::None, operation: ":=".to_string(), value: e}),
}

ExprLogical: Box<Node> = {
    ExprComparison,
    <l: ExprLogical> <op: LogicalOperator> <c: ExprComparison> => expr_operator(op, l, c),
}

ExprComparison: Box<Node> = {
    ExprTerm,
    <c: ExprComparison> <op: ComparisonOperator> <t: ExprTerm> => expr_operator(op, c, t),
}

ExprTerm: Box<Node> = {
    ExprFactor,
    <lt: ExprTerm> <t:Plus> <rt:ExprFactor> => operator_method("__add__", t, lt, rt),
    <lt: ExprTerm> <t:Minus> <rt:ExprFactor> => operator_method("__sub__", t, lt, rt),
    };

ExprFactor: Box<Node> = {
    ExprUnary,
    <l: @L> <lt:ExprFactor> <op:Star> <rt:ExprUnary> <r: @R> => {
        operator_method("__mul__", op, lt, rt)
    },
    <l: @L> <lt:ExprFactor> <op:Slash> <rt:ExprUnary> <r: @R> => {
        operator_method("__div__", op, lt, rt)
    },
}

ExprUnary: Box<Node> = {
    ExprItem,
    <op:Minus> <u: ExprUnary> => unary_operator_method("__neg__", op, u),
    Bang <u: ExprUnary> => negate(u),
}

ExprOptFalseBody: Option<JobListNode> = {
    => None,
    Else Separators? BlockStart <false_body: ExprJobList> BlockEnd => Some(false_body),
}

ExprItem: Box<Node> = {
    ExprModeStart <e:Expr> SubEnd => e,
    SubStart <j:Job> SubEnd => Box::from(Node::Substitution(j.into())),
    <l: Identifier> => Node::identifier(l),
    <l: Regex> => Node::regex(l),
    <l:QuotedFile> => Node::file(l, Quoted),
    <s:QuotedString> => Node::quoted_string(s),
    <s:String> => Node::unquoted_string(s),
    <i:Integer> => Node::integer(i),
    <f:Float> => Node::float(f),
    <e:Return> => Node::return_expr(e),
    <e:Break> => Node::break_expr(e),
    <e:Continue> => Node::continue_expr(e),
    <key:If> <condition:Expr> Separators? BlockStart <true_body: ExprJobList> BlockEnd <false_body: ExprOptFalseBody> => Node::if_expr(key, condition, true_body, false_body),
    <key:While> <condition:Expr> Separators? BlockStart <body: ExprJobList> BlockEnd => Node::while_expr(key, condition, body),
    <key:Loop> Separators? BlockStart <body: ExprJobList> BlockEnd => Node::loop_expr(key, body),
    <key:For> <id:Identifier> Equals <iter:Expr> Separators? BlockStart <body: ExprJobList> BlockEnd => Node::for_expr(key, id.into(), iter, body),
    <i: ExprItem> GetItemStart <e: Assignment> GetItemEnd => Box::from(Node::GetItem(i, e)),
    <i: ExprItem> MemberOperator <l: String> => Box::from(Node::GetAttr(i, l.into())),
    GetItemStart <l: ExprJobList> GetItemEnd => Node::list_literal(l),
    <i: ExprItem> ExprModeStart <ov: ExprOptParamList> SubEnd => {
        let l = i.location();
        let mut v = match ov {
            Some(vv) => vv,
            None => vec![],
        };
                v.insert(0, *i);
                let c = CommandNode {
                    expressions: v,
                    location: l,
                };

                Box::from(Node::Substitution(
                    JobNode{
                        commands: vec![c],
                        location: l,
                    }.into()
                ))
        },
    BlockStart Separators? <s: ExprSignature> <l: ExprJobListWithoutSeparator> BlockEnd => Box::from(Node::Closure(s, l)),
}

ExprOptParamList: Option<Vec<Node>> = {
    => None,
    <e: ExprParamList> Separators? => Some(e),
}

ExprParamList: Vec<Node> = {
    <e: ExprParam> => vec![*e],
    <mut l: ExprParamList> Separators <e: ExprParam> => {
        l.push(*e);
        l
    },
}

ExprParam: Box<Node> = {
    ExprLogical,
    <i: String> <op: Equals> <e: ExprLogical> =>
        Box::from(Node::Assignment{target: Node::unquoted_string(i), style: SwitchStyle::None, operation: "=".to_string(), value: e}),
}


ExprSignature: Option<Vec<ParameterNode>> = {
    => None,
    Pipe Separators? Pipe Separators? => Some(vec![]),
    Pipe Separators? <s: ExprParameterList> Pipe Separators? => Some(s),
}

ExprParameterList: Vec<ParameterNode> = {
    <p: ExprParameter> Separators?=> vec![p],
    <mut l: ExprParameterList> <p: ExprParameter> Separators? => {l.push(p); l},
}

ExprParameter: ParameterNode = {
    <name: String> Equals <value: QuotedString> => ParameterNode::meta(name, value),

    <start: @L> <l: Identifier> <end: @R> <d: ExprDefault>  <doc: QuotedString?> => ParameterNode::parameter(l, None, d, doc),

    <start: @L> <l: Identifier> <end: @R> MemberOperator <t: ExprItem> <d: Default>  <doc: QuotedString?> =>
        ParameterNode::parameter(l, Some(t), d, doc),

    Unnamed <start: @L> <l: Identifier> <end: @R> <doc: QuotedString?> => ParameterNode::unnamed(l, doc),

    Named <start: @L> <l: Identifier> <end: @R>  <doc: QuotedString?> => ParameterNode::named(l, doc),
}

ExprDefault: Option<Node> = {
    => None,
    Equals <l: ExprLogical> => Some(*l),
}

Command: CommandNode = {
    <start: @L> <a: Assignment> <end: @R> => CommandNode{expressions: vec![*a], location: Location::new(start, end)},
    <start: @L> <mut c: Command> <a:Assignment> <end: @R> =>
        {c.expressions.push(*a);
        c.location = c.location.union(Location::new(start, end));
        c
    }
}

Assignment: Box<Node> = {
    Unary,
    <i: Item> <op: AssignmentOperator> <e: Assignment> =>
        Box::from(Node::Assignment{target: i, style: SwitchStyle::None, operation: String::from(op), value: e}),
}

Unary: Box<Node> = {
    Item,
    <op:Unnamed> <u: Item> => Box::from(Node::Unary(TrackedString::new("@", op), u)),
    <op:Named>   <u: Item> => Box::from(Node::Unary(TrackedString::new("@@", op), u)),
}

Signature: Option<Vec<ParameterNode>> = {
    => None,
    Pipe Separators? Pipe Separators? => Some(vec![]),
    Pipe Separators? <s: ParameterList> Pipe Separators? => Some(s),
}

ParameterList: Vec<ParameterNode> = {
    <p: Parameter> Separators? => vec![p],
    <mut l: ParameterList> <p: Parameter> Separators?=> {l.push(p); l},
}

Parameter: ParameterNode = {
    <name: String> Equals <value: QuotedString> => ParameterNode::meta(name, value),

    <start: @L> <l: Identifier> <end: @R> <d: Default> <doc: QuotedString?> => ParameterNode::parameter(l, None, d, doc),

    <start: @L> <l: Identifier> <end: @R> MemberOperator <t: Item> <d: Default> <doc: QuotedString?> =>
        ParameterNode::parameter(l, Some(t), d, doc),

    Unnamed <start: @L> <l: Identifier> <doc: QuotedString?> <end: @R> => ParameterNode::unnamed(l, doc),

    Named <start: @L> <l: Identifier> <doc: QuotedString?> <end: @R> => ParameterNode::named(l, doc),
}

Default: Option<Node> = {
    => None,
    Equals <l: Unary> => Some(*l),
}

Item: Box<Node> = {
    <l:String> => Node::unquoted_string(l),
    <l:File> => Node::file(l, Unquoted),
    <l:Glob> => Node::glob(l),
    <l:Identifier> => Node::identifier(l),
    <l:Regex> => Node::regex(l),
    <l:QuotedFile> => Node::file(l, Quoted),
    <l:QuotedString> => Node::quoted_string(l),
    <l:Integer> => Node::integer(l),
    <l:Float> => Node::float(l),
    <l:Flag> => {
        let ts = TrackedString::from(l);
        Box::from(
            if ts.string.starts_with("--") {
                Node::Assignment{
                    target: Node::unquoted_string(ts.slice_to_end(2)),
                    style: SwitchStyle::Double,
                    operation: "=".to_string(),
                    value: Box::from(Node::Identifier(TrackedString::new("true", ts.location())))
                }
            } else {
                Node::Assignment{
                    target: Node::unquoted_string(ts.slice_to_end(1)),
                    style: SwitchStyle::Single,
                    operation: "=".to_string(),
                    value: Box::from(Node::Identifier(TrackedString::new("true", ts.location())))
            }
        })
    },
    <i: Item> GetItemStart <e: Assignment> GetItemEnd => Box::from(Node::GetItem(i, e)),
    <i: Item> MemberOperator <start: @L> <l: String> <end: @R> => Box::from(Node::GetAttr(i, TrackedString::from(l))),
    BlockStart Separators? <s: Signature> <l: JobListWithoutSeparator> BlockEnd => Box::from(Node::Closure(s, l)),
    SubStart <j:JobList> SubEnd => Box::from(Node::Substitution(j)),
    <l: @L>ExprModeStart <e:ExprJobList> SubEnd <r: @R> => Box::from(Node::Substitution(e)),
}

AssignmentOperator: TrackedString = {
    <l:Equals> => TrackedString::new("=", l),
    <l:Declare> => TrackedString::new(":=", l),
}

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token<'input> {
        LogicalOperator=> Token::LogicalOperator(<&'input str>, <Location>),
        UnaryOperator=> Token::UnaryOperator(<&'input str>, <Location>),
        MemberOperator => Token::MemberOperator(<Location>),
        Equals=> Token::Equals(<Location>),
        Bang=> Token::Bang(<Location>),
        Declare=> Token::Declare(<Location>),
        ComparisonOperator=> Token::ComparisonOperator(<&'input str>, <Location>),
        Slash=> Token::Slash(<Location>),
        Star=> Token::Star(<Location>),
        Plus=> Token::Plus(<Location>),
        Minus=> Token::Minus(<Location>),
        QuotedString=> Token::QuotedString(<&'input str>, <Location>),
        String=> Token::String(<&'input str>, <Location>),
        File=> Token::File(<&'input str>, <Location>),
        Glob=> Token::Glob(<&'input str>, <Location>),
        Identifier=> Token::Identifier(<&'input str>, <Location>),
        Flag=> Token::Flag(<&'input str>, <Location>),
        QuotedFile=> Token::QuotedFile(<&'input str>, <Location>),
        Regex=> Token::Regex(<&'input str>, <Location>),
        Separator=> Token::Separator(<&'input str>, <Location>),
        Integer=> Token::Integer(<&'input str>, <Location>),
        Float=> Token::Float(<&'input str>, <Location>),
        SubStart=> Token::SubStart(<Location>),
        SubEnd=> Token::SubEnd(<Location>),
        BlockStart=> Token::BlockStart(<Location>),
        BlockEnd=> Token::BlockEnd(<Location>),
        GetItemStart=> Token::GetItemStart(<Location>),
        GetItemEnd=> Token::GetItemEnd(<Location>),
        Pipe=> Token::Pipe(<Location>),
        Unnamed=> Token::Unnamed(<Location>),
        Named=> Token::Named(<Location>),
        ExprModeStart=> Token::ExprModeStart(<Location>),
        For => Token::For(<Location>),
        While => Token::While(<Location>),
        Loop => Token::Loop(<Location>),
        If => Token::If(<Location>),
        Else => Token::Else(<Location>),
        Return => Token::Return(<Location>),
        Break => Token::Break(<Location>),
        Continue => Token::Continue(<Location>),
    }
}